plugins {
    id 'com.android.application'

    id 'kotlin-android'
    id 'kotlin-kapt'

    // Google Services
    id 'com.google.gms.google-services'

    // Secrets Gradle Plugin
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
    namespace 'kz.garage'

    compileSdk 34

    defaultConfig {
        applicationId 'kz.garage'

        minSdk 21
        targetSdk 34

        versionCode project.ext.version_code
        versionName project.version
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def googleMapsApiKey = properties.getProperty('google.maps.key')
    if (googleMapsApiKey == null) {
        googleMapsApiKey = 'AIzaSyBArep8mVDm22a0c4If9Vs4nvevN4Tfma8'
    }

    buildTypes {
        debug {
            resValue 'string', 'google_maps_api_key', googleMapsApiKey

            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            resValue 'string', 'google_maps_api_key', googleMapsApiKey

            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion kotlin_version
            }
        }
    }
}

ext {
    coil_version = '2.7.0'
    coroutines_version = '1.10.1'
    okhttp_version = '4.12.0'
    retrofit_version = '2.11.0'
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Activity
    implementation 'androidx.activity:activity-ktx:1.10.0'

    // AppCompat
    implementation 'androidx.appcompat:appcompat:1.7.0'

    // Core
    implementation 'androidx.core:core-ktx:1.15.0'

    // Fragment
    implementation 'androidx.fragment:fragment-ktx:1.8.5'

    // Material Design Components
    implementation 'com.google.android.material:material:1.12.0'

    // Google Play Services
    implementation 'com.google.android.gms:play-services-basement:18.5.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.google.android.gms:play-services-maps:19.0.0'
    implementation 'com.google.maps.android:android-maps-utils:3.10.0'

    // Firebase Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx:22.2.0'

    // LeakCanary
    // debugImplementation because LeakCanary should only run in debug builds
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'

    // OkHTTPClient
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-tls:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Retrofit & relevant converters
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // Coil
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-gif:$coil_version"
    implementation "io.coil-kt:coil-video:$coil_version"

    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Library
    def isRemoteDependencyEnabled = false

    if (isRemoteDependencyEnabled) {
        def libraryVersion = project.version
        implementation "com.github.zhombie.garage:activity:$libraryVersion"
        implementation "com.github.zhombie.garage:animation-funhouse:$libraryVersion"
        implementation "com.github.zhombie.garage:animation-scale:$libraryVersion"
        implementation "com.github.zhombie.garage:chat:$libraryVersion"
        implementation "com.github.zhombie.garage:context:$libraryVersion"
        implementation "com.github.zhombie.garage:drawable:$libraryVersion"
        implementation "com.github.zhombie.garage:file-extension:$libraryVersion"
        implementation "com.github.zhombie.garage:file-size:$libraryVersion"
        implementation "com.github.zhombie.garage:fragment:$libraryVersion"
        implementation "com.github.zhombie.garage:gson:$libraryVersion"
        implementation "com.github.zhombie.garage:image-load:$libraryVersion"
        implementation "com.github.zhombie.garage:image-coil:$libraryVersion"
        implementation "com.github.zhombie.garage:image-preview:$libraryVersion"
        implementation "com.github.zhombie.garage:json:$libraryVersion"
        implementation "com.github.zhombie.garage:kotlin:$libraryVersion"
        implementation "com.github.zhombie.garage:lifecycle-livedata:$libraryVersion"
        implementation "com.github.zhombie.garage:lifecycle-viewmodel:$libraryVersion"
        implementation "com.github.zhombie.garage:locale:$libraryVersion"
        implementation "com.github.zhombie.garage:location-compass:$libraryVersion"
        implementation "com.github.zhombie.garage:location-core:$libraryVersion"
        implementation "com.github.zhombie.garage:location-gms:$libraryVersion"
        implementation "com.github.zhombie.garage:material-tablayout:$libraryVersion"
        implementation "com.github.zhombie.garage:multimedia-store:$libraryVersion"
        implementation "com.github.zhombie.garage:permission:$libraryVersion"
        implementation "com.github.zhombie.garage:popup-tooltip:$libraryVersion"
        implementation "com.github.zhombie.garage:recyclerview:$libraryVersion"
        implementation "com.github.zhombie.garage:text:$libraryVersion"
        implementation "com.github.zhombie.garage:textview:$libraryVersion"
        implementation "com.github.zhombie.garage:view:$libraryVersion"
        implementation "com.github.zhombie.garage:window:$libraryVersion"
    } else {
        implementation project(':activity')
        implementation project(':animation:funhouse')
        implementation project(':animation:scale')
        implementation project(':chat')
        implementation project(':context')
        implementation project(':drawable')
        implementation project(':file:extension')
        implementation project(':file:size')
        implementation project(':fragment')
        implementation project(':gson')
        implementation project(':image:load')
        implementation project(':image:coil')
        implementation project(':image:preview')
        implementation project(':json')
        implementation project(':kotlin')
        implementation project(':lifecycle:livedata')
        implementation project(':lifecycle:viewmodel')
        implementation project(':locale')
        implementation project(':location:compass')
        implementation project(':location:core')
        implementation project(':location:gms')
        implementation project(':material:tablayout')
        implementation project(':multimedia:store')
        implementation project(':permission')
        implementation project(':popup:tooltip')
        implementation project(':recyclerview')
        implementation project(':retrofit:download')
        implementation project(':text')
        implementation project(':textview')
        implementation project(':view')
        implementation project(':window')
    }
}